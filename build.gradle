plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java-library'
    id 'com.google.protobuf' version '0.8.18'
    id 'maven-publish'
    id 'signing'
    id 'idea'
    id "io.github.gradle-nexus.publish-plugin" version '1.1.0'
}

group = 'io.pinecone'
version = '0.2.2' // [pc:VERSION_NEXT]
description = 'The Pinecone.io Java Client'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    jcenter()
}

def grpcVersion = '1.47.0'

dependencies {
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"
    api "io.grpc:grpc-netty:${grpcVersion}"
    runtime 'io.netty:netty-tcnative-boringssl-static:2.0.50.Final'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.google.api.grpc:proto-google-common-protos:2.8.3'
    compileOnly "org.apache.tomcat:annotations-api:6.0.53" // necessary for Java 9+

//    rest deps
    implementation 'io.swagger:swagger-annotations:1.5.24'
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.gsonfire:gson-fire:1.8.4'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
    implementation 'org.threeten:threetenbp:1.4.3'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.hamcrest:hamcrest:2.2"
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

tasks.named('build') {
    dependsOn('shadowJar')
}

// Configure Auto Relocation
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "io.pinecone.shadow" // Default value is "shadow"

}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

publishing {
    publications {
        pineconeClientMaven(MavenPublication) {
            from components.java
            pom {
                artifactId = 'pinecone-client'
                name = 'pinecone-client'
                description = 'The Pinecone.io Java Client'
                url = 'https://github.com/pinecone-io/pinecone-java-client'
                licenses {
                    license {
                        name = 'Proprietary License'
                    }
                }
                developers {
                    developer {
                        id = 'pinecone-ops'
                        name = 'Pinecone.io Ops'
                        email = 'ops@pinecone.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pinecone-io/pinecone-java-client.git'
                    developerConnection = 'scm:git:ssh://github.com/pinecone-io/pinecone-java-client.git'
                    url = 'http://github.com/pinecone-io/pinecone-java-client'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications.pineconeClientMaven
}
